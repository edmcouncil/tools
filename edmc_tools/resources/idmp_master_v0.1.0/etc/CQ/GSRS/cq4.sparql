# CQ 4: Which EV code (future SMS code) does the substance <substance> have?

PREFIX cmns-id: 	<https://www.omg.org/spec/Commons/Identifiers/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
prefix cmns-ra:   	<https://www.omg.org/spec/Commons/RegistrationAuthorities/>
prefix cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
prefix idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/> 
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?substance ?evCode

WHERE {
    # Bind the name we want into a variable 
    BIND("<substance x>" AS ?substanceLabel )

    # Find the substance that is a substance or a subclass of susbtance:
    ?type rdfs:subClassOf* idmp-sub:Substance .
    ?substance rdf:type ?type;

    # has a label that we are looking for
    rdfs:label ?substanceLabel .

    # Find the Registered Identifier or a subclass of registered identifier:
    ?idType rdfs:subClassOf*  cmns-ra:RegisteredIdentifier.
    ?identifier rdf:type  ?idType;

        # Identifies the substance
        cmns-id:identifies ?substance ;

        # has a code
        cmns-txt:hasTextValue ?evCode ;
  
        # Registered in the EMA
        cmns-ra:isRegisteredIn idmp-ra:EuropeanMedicinesAgencyRegistrationAuthority .
}
