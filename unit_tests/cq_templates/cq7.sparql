# CQ 7: Which manufactured items contain substance <x> as ingredient of type “active”?

prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-pts:    <https://www.omg.org/spec/Commons/PartiesAndSituations/>
prefix idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT distinct ?manufacturedItem ?manufacturedItemLabel ?substanceInActiveIngredient

WHERE {
	# Bind the name we want into a variable
	BIND(cq_7_parameter_1 AS ?substanceLabel) 

  	# Find Manufactured Items
	?manufacturedItemType rdfs:subClassOf* idmp-sub:ManufacturedItem .
  	?manufacturedItem rdf:type ?manufacturedItemType .
  	?manufacturedItem rdfs:label ?manufacturedItemLabel .
  
  	# Find products that include the manufactured item based on their composition
	?manufacturedItem cmns-dsg:isDefinedIn ?productComposition .

  	# product compositions link to substances as ingredients of the pharmaceutical product
  	?productComposition cmns-dsg:defines ?pharmaceuticalProduct .
  	?productComposition idmp-mprd:hasActiveIngredient ?substanceInActiveIngredient .

  	# Substance as an active ingredient
  	?substanceInActiveIngredient cmns-pts:isPlayedBy ?substance .
  	?substance rdfs:label ?substanceLabel .
}